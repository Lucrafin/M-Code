let KontoutdragManuall = ( optional CheckFileName as logical ) as table =>

    let

        SourceAccTran = Folder.Files( "\\Skandianet.org\common\SE\LINK\Depaforsakring\WD\Kontoutdrag\2025\2501" ),
        //SourceAccTran = Folder.Files( "\\office\aktiehandel\BackOffice Värdepapper Ekonomi\Filer\Test för Workday\Sally_TPS" ),

        // Extract file path from metadata after applying filter to the 'Name' column
        // and take first row of table sorted by 'Date modified' column
        FilePath =
            Table.CombineColumns(
                Table.FirstN(
                    Table.Sort(
                        Table.SelectRows( 
                            SourceAccTran, 
                            each if CheckFileName = true ?? false then Text.Contains( Text.Lower( [Name] ), "kontoutdrag" ) else true
                        ),
                        { "Date modified", Order.Descending }
                    ),
                    1
                ),
                { "Folder Path", "Name" },
                Combiner.CombineTextByDelimiter("", QuoteStyle.None),
                "File Path"
            ){0}[File Path],
        
        ExcelFile = 
            Table.PromoteHeaders( 
                Excel.Workbook(
                    File.Contents(FilePath), null, true
                ){0}[Data],
                [PromoteAllScalars=true] 
            ),

        FilterManual = 
            Table.Distinct(
                Table.SelectRows( 
                    Table.FillDown( ExcelFile, { "Ledger Account" } ),
                    each not Text.Contains( Text.Lower( [Ledger Account] ), "total" ) and [Accounting Date] <> null and Text.Contains( Text.Lower( [Journal Source] ), "manual" )
                ), {"Journal Sequence Number"}
            ),
            
        SplitLA = 
            Table.SplitColumn(
                FilterManual, 
                "Ledger Account", 
                Splitter.SplitTextByDelimiter(":", QuoteStyle.Csv), 
                {"Account Number", "Account Name"}
            ),

        SelectColumns = 
            Table.SelectColumns(
                SplitLA,
                { "Journal Sequence Number", "Memo", "Line Memo" }),
                
        ChangedType = 
            Table.TransformColumnTypes(
                SelectColumns,
                { 
                    {"Line Memo", type text}, 
                    {"Memo", type text},
                    {"Journal Sequence Number", type text}
                }
            )

    in
        ChangedType,

        documentation = [
            Documentation.Name =  " fxKontoutdragManuell ",
            Documentation.Description = " Work with Workday files and transform them into the appropriate format for later use. ",
            Documentation.LongDescription = " The function extracts the file from this path '\\Skandianet.org\common\SE\LINK\Depaforsakring\WD\Kontoutdrag\2025\2501'. CheckFileName is optional and has to be 'true' if you want to search after the word 'kontoutdrag' in the file name. Then it only keeps 'Manual Journals'. ",
            Documentation.Category = " Workday ",
            Documentation.Source = " GitHub Lucrafin ",
            Documentation.Version = " 1.0 ",
            Documentation.Author = " Aleksandar Kovacevic "
        ]
in 
    Value.ReplaceType(KontoutdragManuall, Value.ReplaceMetadata(Value.Type(KontoutdragManuall), documentation) )
